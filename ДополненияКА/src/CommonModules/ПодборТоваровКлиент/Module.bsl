//
//	Филимонов И.В.
//		+7 913 240 81 77
//		+7 905 084 20 06 (Telegram)
//		https://github.com/Shu-ler
//		

//@skip-check extension-variable-prefix
//@skip-check wrong-string-literal-content
#Область ПрограммныйИнтерфейс

// Устанавливает текущую строку иерархии номенклатуры в формах списков номенклатуры и подборов.
// 
// Параметры:
//	Форма - см. ПодборТоваровСервер.ПриСозданииФормыПодбораНаСервере.Форма
//
&После("УстановитьТекущуюСтрокуИерархииНоменклатуры")
Процедура ДКА_УстановитьТекущуюСтрокуИерархииНоменклатуры(Форма) Экспорт
	
	// Для механизма отображения цен, остатков и поддержания запасов в списке номенклатуры 
	Если Форма.ИмяФормы = "Справочник.Номенклатура.Форма.ФормаСписка" Тогда
		ДКА_СобытияФормКлиент.УстановитьТекущуюСтрокуИерархииНоменклатуры(Форма);
	КонецЕсли;

КонецПроцедуры

&ИзменениеИКонтроль("ИзменитьВариантНавигации")
Процедура ДКА_ИзменитьВариантНавигации(Форма) Экспорт

	СписокВыбораВариантовНавигации = Новый СписокЗначений;
	СписокВыбораВариантовНавигации.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыНавигацииВФормахНоменклатуры.ПоВидамИСвойствам"),
											НСтр("ru = 'Навигация по видам и свойствам'"));
	СписокВыбораВариантовНавигации.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыНавигацииВФормахНоменклатуры.ПоИерархии"),
											НСтр("ru = 'Навигация по иерархии'"));
#Вставка 
	Если Форма.ИмяФормы = "Справочник.Номенклатура.Форма.ФормаСписка" Тогда
		СписокВыбораВариантовНавигации.Добавить(НСтр("ru = 'РасширенныйПросмотр'"),
												НСтр("ru = 'Расширенный просмотр'"),
												Форма.ДКА_РасширенныйПросмотр);
	КонецЕсли;
#КонецВставки

	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьВариантНавигацииЗавершение", Форма);

	Форма.ПоказатьВыборИзМеню(ОписаниеОповещения,
								СписокВыбораВариантовНавигации,
								Форма.Элементы.КоманднаяПанельВариантНавигации);

КонецПроцедуры

#КонецОбласти
