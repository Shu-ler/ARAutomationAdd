//
//	Филимонов И.В.
//		+7 913 240 81 77
//		+7 905 084 20 06 (Telegram)
//		https://github.com/Shu-ler
//		
#Область ПрограммныйИнтерфейс

// Процедура, вызываемая из одноименного обработчика события формы.
//
// Параметры:
//  Форма					- ФормаКлиентскогоПриложения - Форма, из обработчика события которой происходит вызов процедуры.
//  Отказ					- Булево - Признак отказа от создания формы.
//  СтандартнаяОбработка	- Булево - Признак выполнения стандартной (системной) обработки события
//  ДополнительныеПараметры	- Структура - Дополнительные параметры.
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка, ДополнительныеПараметры = Неопределено) Экспорт

	ИзменитьОтображениеКомандФормы(Форма);

	Если Форма.Параметры.Свойство("Заголовок") Тогда
		Форма.АвтоЗаголовок = Не Форма.Параметры.Свойство("Заголовок", Форма.Заголовок);
	КонецЕсли;
	
//	// Изменение расположения заголовков страниц
//	Если ПолучитьФункциональнуюОпцию("РасполагатьЗакладкиСтраницСлева") Тогда
//		ОтобразитьЗаголовкиСтраницСлева(Форма);
//	КонецЕсли;
//
//	// Отображение в заголовках страниц количества строк в таблицах формы на страницах
//	Если ПолучитьФункциональнуюОпцию("УказыватьКоличествоСтрокВТаблицахФорм") Тогда
//		ОтобразитьКоличествоСтрокВТаблицахФормы(Форма);
//	КонецЕсли;

КонецПроцедуры

// Добавляет к заголовку страницы количество строк таблицы формы на этой странице
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//  ТаблицаФормы - ТаблицаФормы - Таблица формы
//  ГруппаФормы - ГруппаФормы - Группа формы
Процедура ДобавитьКоличествоВЗаголовокСтраницы(Форма, ТаблицаФормы, ГруппаФормы) Экспорт

	ПрефиксОбъект = "Объект.";

	ПутьКДанным = ТаблицаФормы.ПутьКДанным;

	Если СтрНачинаетсяС(ПутьКДанным, ПрефиксОбъект) Тогда

		// Получение текущего заголовка страницы
		Заголовок = ГруппаФормы.Заголовок;

		// Получение количества строк в табличной части объекта
		ИмяТЧ = СтрЗаменить(ПутьКДанным, ПрефиксОбъект, "");
		Количество = Форма.Объект[ИмяТЧ].Количество();

		// Удаление ранее установленного количества в скобках
		ИндексСкобки = СтрНайти(Заголовок, "(", НаправлениеПоиска.СКонца);
		Если ИндексСкобки > 0 Тогда
			Заголовок = Лев(Заголовок, ИндексСкобки - 2);
		КонецЕсли;

		// Формирование и установка нового заголовка
		Заголовок = ?(Количество = 0, Заголовок, СтрШаблон("%1 (%2)", Заголовок, Формат(Количество, "ЧГ=0;")));
		ГруппаФормы.Заголовок = Заголовок;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Для кнопок "Записать" и "Провести" устанавливает отображение в виде стандартной картинки
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ИзменитьОтображениеКомандФормы(Форма)

	КнопкаЗаписать = Форма.Элементы.Найти("ФормаЗаписать");

	Если КнопкаЗаписать <> Неопределено Тогда
		КнопкаЗаписать.Картинка = БиблиотекаКартинок.Записать;
		КнопкаЗаписать.Отображение = ОтображениеКнопки.Картинка;
	КонецЕсли;

	КнопкаПровести = Форма.Элементы.Найти("ФормаПровести");

	Если КнопкаПровести <> Неопределено Тогда
		КнопкаПровести.Картинка = БиблиотекаКартинок.Провести;
		КнопкаПровести.Отображение = ОтображениеКнопки.Картинка;
	КонецЕсли;

КонецПроцедуры

// При наличии группы "Страницы" изменяет расположение заголовков страниц
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//@skip-check module-unused-method
Процедура ОтобразитьЗаголовкиСтраницСлева(Форма)

	Перем ГруппаСтраницы;

	ГруппаСтраницы = Форма.Элементы.Найти("Страницы");

	Если ТипЗнч(ГруппаСтраницы) = Тип("ГруппаФормы") И ГруппаСтраницы.Вид = ВидГруппыФормы.Страницы Тогда
		ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСлеваГоризонтально;
	КонецЕсли;

КонецПроцедуры

// Отображает количество строк в таблицах формы в заголовках страниц, на которых расположены таблицы.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//@skip-check module-unused-method
Процедура ОтобразитьКоличествоСтрокВТаблицахФормы(Форма)

	Для Каждого ТекущийЭлемент Из Форма.Элементы Цикл

		Если ТипЗнч(ТекущийЭлемент) = Тип("ГруппаФормы") Тогда

			Для Каждого ПодчиненныйЭлемент Из ТекущийЭлемент.ПодчиненныеЭлементы Цикл
				Если ТипЗнч(ПодчиненныйЭлемент) = Тип("ТаблицаФормы") Тогда
					ДобавитьКоличествоВЗаголовокСтраницы(Форма, ПодчиненныйЭлемент, ТекущийЭлемент);
				КонецЕсли;
			КонецЦикла;

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

#КонецОбласти