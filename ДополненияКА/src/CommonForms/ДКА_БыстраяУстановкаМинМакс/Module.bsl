//
//	Филимонов И.В.
//		+7 913 240 81 77
//		+7 905 084 20 06 (Telegram)
//		https://github.com/Shu-ler
//		
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Возврат при получении формы для анализа
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Номенклатура = Параметры["Номенклатура"];

	ТекстЗапроса = "ВЫБРАТЬ
				   |	ЕСТЬNULL(ТоварныеОграничения.МинимальноеКоличествоЗапаса, 0) КАК Мин,
				   |	ЕСТЬNULL(ТоварныеОграничения.МаксимальноеКоличествоЗапаса, 0) КАК Макс,
				   |	Склады.Ссылка КАК Склад,
				   |	Склады.Наименование КАК НаименованиеСклада
				   |ИЗ
				   |	Справочник.Склады КАК Склады
				   |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТоварныеОграничения КАК ТоварныеОграничения
				   |		ПО ТоварныеОграничения.Склад = Склады.Ссылка
				   |		И ТоварныеОграничения.Номенклатура = &Номенклатура
				   |ГДЕ
				   |	Склады.ПометкаУдаления = ЛОЖЬ
				   |	И Склады.ЭтоГруппа = ЛОЖЬ
				   |
				   |УПОРЯДОЧИТЬ ПО
				   |	Макс УБЫВ,
				   |	НаименованиеСклада";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		СтрокаСписок = Список.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСписок, Выборка);
	КонецЦикла;

	Элементы.Список.АвтоМаксимальнаяВысота = Ложь;
	Элементы.Список.ВысотаВСтрокахТаблицы = Выборка.Количество();

	// Вызов стандартной для конфигунации обработки события
	ДКА_СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	ОчиститьСообщения();
	СохранитьИЗакрытьНаСервере();
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьИЗакрытьНаСервере()
	
	НаборЗаписей = РегистрыСведений.ТоварныеОграничения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура, Истина);
	НаборЗаписей.Записать();
	
	Для Каждого Эл Из Список Цикл
		
		Если Эл.Мин <> 0 Или Эл.Макс <> 0 Тогда
			
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Номенклатура = Номенклатура;
			НоваяЗапись.Склад = Эл.Склад;
			НоваяЗапись.МинимальноеКоличествоЗапаса = Эл.Мин;
			НоваяЗапись.МаксимальноеКоличествоЗапаса = Эл.Макс;
			НоваяЗапись.МетодОбеспеченияПотребностей = Перечисления.МетодыОбеспеченияПотребностей.ПоддержаниеЗапасаМинМакс;
			НоваяЗапись.ОбеспечениеЗаказовПриПоддержанииЗапаса = Перечисления.ОбеспечениеЗаказовПриПоддержанииЗапаса.ЗаСчетЗапасов;
			НоваяЗапись.Активность = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти
