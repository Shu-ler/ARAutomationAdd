// @strict-types
#Область ПрограммныйИнтерфейс

// Обработчик одноименного события для общего модуля ПодборТоваровКлиент
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//@skip-check dynamic-access-method-not-found
//@skip-check invocation-parameter-type-intersect
Процедура УстановитьТекущуюСтрокуИерархииНоменклатуры(Форма) Экспорт

	ИмяРеквизитаРасширенныйПросмотр = "ДКА_РасширенныйПросмотр";
	ИмяПанели = "ДКА_ПанельОстаткиЦены";

	Если Форма[ИмяРеквизитаРасширенныйПросмотр] Тогда
		
		// Разыменования
		ИмяСписка = ПодборТоваровКлиентСервер.ИмяСпискаНоменклатурыПоВариантуПоиска(Форма);
		Номенклатура = Форма.Элементы[ИмяСписка].ТекущаяСтрока;
		
		ИмяТаблицыЦен = "ДКА_Цены";
		ИмяТаблицыОстатки = "ДКА_Остатки";
		ИмяРеквизитаПрименимость = "ДКА_Применимость";
		ИмяРеквизитаАдресКартинки = "ДКА_АдресКартинки";
		ИмяТаблицыЗапасы = "ДКА_Запасы";
		
		РеквизитТаблицаЦен = Форма[ИмяТаблицыЦен];
		РеквизитТаблицаОстатки = Форма[ИмяТаблицыОстатки];
		РеквизитТаблицаЗапасы = Форма[ИмяТаблицыЗапасы];

		// Получение данных для расширенного представления
		Данные = ДКА_СобытияФормВызовСервера.ДанныеРасширенногоПросмотра(Номенклатура);
		
		// Перенос цен в реквизит формы ДКА_Цены
		РеквизитТаблицаЦен.Очистить();

		Для Каждого Эл Из Данные.Цены Цикл
			ЗаполнитьЗначенияСвойств(РеквизитТаблицаЦен.Добавить(), Эл);
		КонецЦикла;

		// Перенос цен в реквизит формы ДКА_Цены
		РеквизитТаблицаОстатки.Очистить();

		Для Каждого Эл Из Данные.Остатки Цикл
			ЗаполнитьЗначенияСвойств(РеквизитТаблицаОстатки.Добавить(), Эл);
		КонецЦикла;

		// Перенос товарных ограничений в реквизит формы ДКА_Запасы
		РеквизитТаблицаЗапасы.Очистить();

		Для Каждого Эл Из Данные.Запасы Цикл
			ЗаполнитьЗначенияСвойств(РеквизитТаблицаЗапасы.Добавить(), Эл);
		КонецЦикла;

		// Получение применимости
		Форма[ИмяРеквизитаПрименимость] = Данные.Применимость;
		
		// Получение картинки
		Форма[ИмяРеквизитаАдресКартинки] = Данные.Картинка;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти