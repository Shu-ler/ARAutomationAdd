//
//	Филимонов И.В.
//		+7 913 240 81 77
//		+7 905 084 20 06 (Telegram)
//		https://github.com/Shu-ler
//		
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Возврат при получении формы для анализа
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	// Получение имен для формы
	Имена = ДКА_ФормыОбъектовКлиентСервер.КонстантыФормыСпискаНоменклатуры();

	// Получение текущих настроек
	НастройкиПанели = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(Имена.КлючОбъекта, "");

	ПоказатьЗапасы = НастройкиПанели.ПоказатьЗапасы;
	ПоказатьПрименимость = НастройкиПанели.ПоказатьПрименимость;
	ПоказатьКартинку = НастройкиПанели.ПоказатьКартинку;

	КлючОбъекта = Имена.КлючОбъекта;
	
	Для Каждого Эл Из НастройкиПанели.ВидыЦен Цикл
		ВидыЦен.Добавить(Эл.Значение, , Истина);
	КонецЦикла;

	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
				   |	ВидыЦен.Ссылка КАК Ссылка
				   |ИЗ
				   |	Справочник.ВидыЦен КАК ВидыЦен
				   |ГДЕ
				   |	ВидыЦен.Ссылка НЕ В (&ИсключитьВидыЦен)
				   |
				   |УПОРЯДОЧИТЬ ПО
				   |	ВидыЦен.Наименование";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ИсключитьВидыЦен", НастройкиПанели.ВидыЦен);

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ВидыЦен.Добавить(Выборка.Ссылка, , Ложь);
	КонецЦикла;

	ДлинаСписка = ВидыЦен.Количество();

	Элементы.ВидыЦен.ВысотаВСтрокахТаблицы = ДлинаСписка;
	 
	// Вызов стандартной для конфигунации обработки события
	ДКА_СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)

	Модифицированность = Истина;
	
	СписокВидов = Новый СписокЗначений;

	Для Каждого Эл Из ВидыЦен Цикл
		Если Эл.Пометка Тогда
			СписокВидов.Добавить(Эл.Значение);
		КонецЕсли;
	КонецЦикла;

	Настройки = Новый Структура;

	Настройки.Вставить("ПоказатьЗапасы", ПоказатьЗапасы);
	Настройки.Вставить("ПоказатьПрименимость", ПоказатьПрименимость);
	Настройки.Вставить("ПоказатьКартинку", ПоказатьКартинку);
	Настройки.Вставить("ВидыЦен", СписокВидов);
	
	СохранитьНастройки(КлючОбъекта, Настройки);
	
//	КодВозврата = ?(Модифицированность, КодВозвратаДиалога.ОК, КодВозвратаДиалога.Отмена);
//	Закрыть(КодВозврата);

	Закрыть(Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СохранитьНастройки(КлючОбъекта, Настройки)
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючОбъекта, "", Настройки);
КонецПроцедуры

#КонецОбласти